import macros, os, strutils, tables, times

import compiler/[ast, astalgo, idents, options, renderer]

import "."/treesitter/api

import "."/[compat, globals, getters]

proc addConst(nimState: NimState, node: TSNode) =
  # (preproc_def
  #  (identifier)
  #  (preproc_arg)
  # )
  nimState.printDebug(node)

  if node[0].getName() == "identifier" and
    node[1].getName() == "preproc_arg":
    let
      constDef = newNode(nkConstDef)

      # node[0] = identifier = const name
      (name, info) = nimState.getNameInfo(node.getAtom(), nskConst)
      # TODO - check blank and override
      ident = nimState.getIdent(name, info)

      # node[1] = preproc_arg = value
      val = nimState.getNodeVal(node[1]).getLit()

    # If supported literal
    if val.kind != nkNilLit:
      # nnkConstDef(
      #   nnkPostfix(
      #     nnkIdent("*"),
      #     nnkIdent(name)
      #   ),
      #   nnkEmpty(),
      #   nnkXLit(val)
      # )
      constDef.add ident
      constDef.add newNode(nkEmpty)
      constDef.add val

      # nnkConstSection.add
      nimState.constSection.add constDef

      nimState.printDebug(constDef)

proc addType(nimState: NimState, node: TSNode) =
  # CASE1:
  #
  # typedef struct X Y;
  #
  # (type_definition
  #  (struct_specifier
  #   (type_identifier)
  #  )
  #  (type_identifier)
  # )
  #
  # CASE 2
  #
  # typedef struct X *Y;
  #
  # (type_definition
  #  (struct_specifier
  #   (type_identifier)
  #  )
  #  (pointer_declarator
  #   (type_identifier)
  #  )
  # )
  #
  nimState.printDebug(node)

  let
    typeDef = newNode(nkTypeDef)

    fdlist = node.inTree("field_declaration_list")

    identNode = if fdlist: node[1] else: node

    (name, info) = nimState.getNameInfo(identNode.getAtom(), nskType)
    # TODO - check blank and override
    ident = nimState.getIdent(name, info)

    typeNode =
      if fdlist: node[0] else: node[1]


proc addEnum(nimState: NimState, node: TSNode) =
  nimState.printDebug(node)

proc addProc(nimState: NimState, node: TSNode) =
  nimState.printDebug(node)

proc processNode(nimState: NimState, node: TSNode): bool =
  result = true

  case node.getName()
  of "preproc_def":
    nimState.addConst(node)
  of "type_definition":
    if node.inTree("struct_specifier"):
      nimState.addType(node)
    elif node.inTree("enum_specifier"):
      nimState.addEnum(node)
    else:
      # Unknown type
      result = false
  of "struct_specifier":
    nimState.addType(node)
  of "enum_specifier":
    nimState.addEnum(node)
  of "declaration":
    nimState.addProc(node)
  else:
    # Unknown
    result = false

proc searchTree(nimState: NimState, root: TSNode) =
  # Search AST generated by tree-sitter for recognized elements
  var
    node = root
    nextnode: TSNode
    depth = 0
    processed = false

  while true:
    if not node.tsNodeIsNull() and depth > -1:
      processed = nimState.processNode(node)
    else:
      break

    if not processed and node.len() != 0:
      nextnode = node[0]
      depth += 1
    else:
      nextnode = node.tsNodeNextNamedSibling()

    if nextnode.tsNodeIsNull():
      while true:
        node = node.tsNodeParent()
        depth -= 1
        if depth == -1:
          break
        if node == root:
          break
        if not node.tsNodeNextNamedSibling().tsNodeIsNull():
          node = node.tsNodeNextNamedSibling()
          break
    else:
      node = nextnode

    if node == root:
      break

proc printNimHeader*() =
  echo """# Generated at $1
# Command line:
#   $2 $3

{.hint[ConvFromXtoItselfNotNeeded]: off.}

import nimterop/types
""" % [$now(), getAppFilename(), commandLineParams().join(" ")]

proc printNim*(gState: State, fullpath: string, root: TSNode) =
  var
    nimState = new(NimState)
    fp = fullpath.replace("\\", "/")

  nimState.identifiers = newTable[string, string]()

  nimState.gState = gState
  nimState.currentHeader = getCurrentHeader(fullpath)
  nimState.sourceFile = fullpath

  # Nim compiler objects
  nimState.identCache = newIdentCache()
  nimState.config = newConfigRef()

  nimState.constSection = newNode(nkConstSection)
  nimState.enumSection = newNode(nkStmtList)
  nimState.procSection = newNode(nkStmtList)
  nimState.typeSection = newNode(nkTypeSection)

  nimState.searchTree(root)

  var
    tree = newNode(nkStmtList)
  tree.add nimState.enumSection
  tree.add nimState.constSection
  tree.add nimState.typeSection
  tree.add nimState.procSection

  echo tree.renderTree()
